# Maintainer:  fincs <fincs.alt1@gmail.com>

pkgbasename=glfw
pkgname=switch-$pkgbasename
pkgver=3.3.2
pkgrel=3
pkgdesc='Open Source, multi-platform library for OpenGL, OpenGL ES and Vulkan development on the desktop'
arch=('any')
url='https://github.com/glfw/glfw'
license=(MIT)
options=(!strip libtool staticlibs)
source=(
  "${url}/releases/download/${pkgver}/${pkgbasename}-${pkgver}.zip"
  "${pkgname}-${pkgver}.patch"
)
sha256sums=(
  '08a33a512f29d7dbf78eab39bd7858576adcc95228c9efe8e4bc5f0f3261efc7'
  '8ee1e2535016c44796896852bc06c11ddd7b24ccf61c6b7a97d33ed076383378'
)
depends=('switch-mesa')
makedepends=('dkp-toolchain-vars' 'switch-cmake')
groups=('switch-portlibs')

build() {

  cd ${pkgbasename}-${pkgver}
  patch -p1 -i ${srcdir}/${pkgname}-${pkgver}.patch

  source ${DEVKITPRO}/switchvars.sh

  aarch64-none-elf-cmake -G"Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_AR="/opt/devkitpro/devkitA64/bin/aarch64-none-elf-gcc-ar" \
    -DGLFW_BUILD_EXAMPLES:BOOL=OFF -DGLFW_BUILD_TESTS:BOOL=OFF -DGLFW_BUILD_DOCS:BOOL=OFF \
    -DGLFW_VULKAN_STATIC:BOOL=ON -DGLFW_EGL_STATIC:BOOL=ON \
    .

  make

}

package() {
  cd ${pkgbasename}-${pkgver}

  make install DESTDIR="$pkgdir"
}
